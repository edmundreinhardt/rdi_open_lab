     A*****************************************************************
     A*   PEFMST - REFERENCE MASTER
     A*   DESCRIPTION - THIS FILE CONTAINS FIELD LEVEL INFORMATION FOR THE
     A*   TIME REPORTING SYSTEM FILES.  THESE DEFINITIONS WILL BE REFERENCED
     A*   BY WHEN CREATING THE SPECIFIC PHYSICAL AND LOGICAL FILES AS WELL
     A*   BY SDA WHEN CREATING DISPLAY FORMATS.
     A*****************************************************************
     A          R RCREF                     TEXT('DATA BASE REFERENCE')
     A*  COMMON FIELDS
     A            ACREC          1          TEXT('ACTIVE RECORD CODE')
     A                                      COLHDG('ACREC')
     A            DATFL          6  0       TEXT('DATE FIELD')
     A                                      COLHDG('DATE' 'MMDDYY')
     A*  EMPLOYEE MASTER RELATED FIELDS
     A            EMPNO          6  0       TEXT('EMPLOYEE NUMBER')
     A                                      COLHDG('EMPLOYEE' 'NUMBER')
     A            ENAME         30          TEXT('EMPLOYEE NAME')
     A                                      COLHDG('EMPLOYEE' 'NAME')
     A            EMCAT          1          TEXT('EMPLOYEE CATEGORY')
     A                                      COLHDG('EMP' 'CAT')
     A            EDEPT          5          TEXT('EMPLOYEE DEPARTMENT')
     A                                      COLHDG('EMPL' 'DEPT')
     A            ELOCN         30          TEXT('EMPLOYEE LOCATION')
     A                                      COLHDG('EMPLOYEE' 'LOCATION')
     A            EUSRI          8          TEXT('EMPLOYEE USRID')
     A                                      COLHDG('EMPLOYEE' 'USRID')
     A            ENHRS          3  1       TEXT('EMPLOYEE NORMAL WEEK HOURS')
     A                                      COLHDG('NORMAL' 'WK HRS')
     A            EPHRC          5  1       TEXT('PROJECT HOURS CURRENT MONTH')
     A                                      COLHDG('PRJ HRS' 'CUR MTH')
     A            EPHRY          7  1       TEXT('PROJECT HOURS YEAR TO DATE')
     A                                      COLHDG('PRJ HRS' 'YTD')
     A            EPHRP          7  1       TEXT('PROJECT HOURS PRIOR YEAR')
     A                                      COLHDG('PRJ HRS' 'PRIOR YR')
     A            EPNRC          5  1       TEXT('NON PROJECT HOURS CURR MONTH')
     A                                      COLHDG('NON PRJ HRS' 'CUR MTH')
     A            EPNRY          7  1       TEXT('NON PROJECT HOURS YTD')
     A                                      COLHDG('NON PRJ' 'HRS YTD')
     A            EPNRP          7  1       TEXT('NON PROJECT HOURS PRIOR YEAR')
     A                                      COLHDG('NON PRJ HRS' 'PRIOR YR')
     A            EHWRK          5  1       TEXT('EMPLOYEE HOURS WORKED')
     A                                      COLHDG('EMP HRS' 'WORKED')
     A*  PROJECT MASTER RELATED FIELDS
     A            PRCDE          8          TEXT('PROJECT CODE')
     A                                      COLHDG('PROJECT' 'CODE')
     A            PRDSC         50          TEXT('PROJECT DESCRIPTION')
     A                                      COLHDG('PROJECT' 'DESCRIPTION')
     A            PRRSP         30          TEXT('PROJECT RESPONSIBILITY')
     A                                      COLHDG('PROJECT' 'RESPONSIBILITY')
     A            PRSTR     R               REFFLD(DATFL)
     A                                      TEXT('PROJECT START DATE')
     A                                      COLHDG('PRJ START' 'DATE')
     A            PREND     R               REFFLD(DATFL)
     A                                      TEXT('PROJECT ESTIMATED END DATE')
     A                                      COLHDG('PRJ EST' 'END DATE')
     A            PRCMP     R               REFFLD(DATFL)
     A                                      TEXT('PROJECT COMPLETION DATE')
     A                                      COLHDG('PRJ CMP' 'DATE')
     A            PREST          9  1       TEXT('PROJECT ESTIMATED TOTAL HRS')
     A                                      COLHDG('PRJ EST' 'TOT HRS')
     A            PRHRC          7  1       TEXT('PROJECT HOURS CURRENT MONTH')
     A                                      COLHDG('PRJ HRS' 'CUR MTH')
     A            PRHRY          9  1       TEXT('PROJECT HOURS YEAR TO DATE')
     A                                      COLHDG('PRJ HRS' 'YTD')
     A            PRHRP          9  1       TEXT('PROJECT HOURS PRIOR YEAR')
     A                                      COLHDG('PRJ HRS' 'PRIOR YR')
     A*  REASON CODE MASTER RELATED FIELDS
     A            RSCDE          8          TEXT('REASON CODE')
     A                                      COLHDG('REASON' 'CODE')
     A            RSDSC         50          TEXT('REASON CODE DESCRIPTION')
     A                                      COLHDG('REASON CODE' 'DESCRIPTION')
     A            RSHRC          7  1       TEXT('REASON CODE HRS CURR MONTH')
     A                                      COLHDG('RSN CDE HRS' 'CUR MTH')
     A            RSHRY          9  1       TEXT('REASON CODE HRS YEAR TO DATE')
     A                                      COLHDG('RSN CDE' 'HRS YTD')
     A            RSHRP          9  1       TEXT('REASON CODE HOURS PRIOR YEAR')
     A                                      COLHDG('RSN CDE HRS' 'PRIOR YR')
     A*  CONTROL FILE RELATED FIELDS
     A            CTCDE          6          TEXT('CONTROL RECORD CODE')
     A                                      COLHDG('CTL REC' 'CODE')
     A            CWKDT          6S 0       TEXT('WEEK ENDING DATE')
     A                                      COLHDG('WEEK END' 'DATE')
     A            CMTDT          6S 0       TEXT('MONTH ENDING DATE')
     A                                      COLHDG('MTH END' 'DATE')
     A            CALLE          1          TEXT('ALL ENTRIES MADE FLAG')
     A                                      COLHDG('ENTRIES' 'FLAG')
     A*  TIME ENTRY TRANSACTION RELATED
     A            ACDAT          6S 0       TEXT('ACTUAL DATE WORKED')
     A                                      COLHDG('ACTUAL DATE' 'WORKED')
     A            TFRRN          5  0       TEXT('TRANSACTION FILE RRN')
     A                                      COLHDG('RELATIVE' 'RECORD NO')
